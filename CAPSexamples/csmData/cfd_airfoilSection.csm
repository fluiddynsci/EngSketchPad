# Example Cart3D input file to create a simple wing model
#
# -----------------------------------------------------------------
# Define the AIMs that the geometry is intended support
# -----------------------------------------------------------------
attribute capsAIM    $cart3dAIM

# -----------------------------------------------------------------
# Design parameters to define the wing cross section and planform 
# -----------------------------------------------------------------
despmtr   thick     0.12      frac of local chord
despmtr   camber    0.00      frac of local chord
despmtr   area      10.0      Planform area of the full span wing
despmtr   aspect    4.00      Span^2/Area
despmtr   taper     0.60      TipChord/RootChord
despmtr   twist     0.00      Airfoil twist angle
cfgpmtr   aerosystem        1

# -----------------------------------------------------------------
# set parameters for use internally to create geometry
# -----------------------------------------------------------------

set       span      sqrt(aspect*area)
set       croot     2*area/span/(1+taper)
set       ctip      croot*taper
set       semispan  span/2

# -----------------------------------------------------------------
# 1. Create Airfoils using the User Defined Primitive (UDP) naca
#  > The inputs used for this example to the UDP are Thickness and Camber
#  > Cross sections are in the X-Y plane and are rotated to the X-Z plane if aerosystem
# -----------------------------------------------------------------

mark
udprim    naca      Thickness thick     Camber    camber sharpte 1
scale     croot
udprim    naca      Thickness thick     Camber    camber sharpte 1
scale     ctip
rotatez  -twist     ctip     0
translate 0         0        semispan
blend  0   -1;1

# Trigger TFI templates
select edge @nbody 3 @nbody 4 1
select add  @nbody 3 @nbody 4 2
  attribute .nPos 40

select edge @nbody 1 @nbody 4 1
select add  @nbody 1 @nbody 3 1
select add  @nbody 2 @nbody 3 1
select add  @nbody 2 @nbody 4 1
  attribute .nPos 180

ifthen aerosystem eq 1
    rotatex   90        0        0
endif

select face
  attribute capsGroup          $Wing
select body
  attribute capsReferenceArea  area
  attribute capsReferenceSpan  span
  attribute capsReferenceChord croot
  attribute capsReferenceX     croot/4
