# Example AWAVE input file to create a simple wing model
#
# Note to execute in serveCSM a dictionary file must be included
# serveCSM -dict $ESP_ROOT/include/intent.dict awaveWingTailFuselage.csm

# -----------------------------------------------------------------
# Define the analysis fidelity that the geometry is intended support
# -----------------------------------------------------------------
## [capsAIM]
attribute capsAIM $awaveAIM
## [capsAIM]

# -----------------------------------------------------------------
# Defines units of *.csm input file, optional as AWAVE is nondimensional
# -----------------------------------------------------------------
## [capsLength]
attribute capsLength $m
## [capsLength]

# -----------------------------------------------------------------
# Design parameters to define the wing cross section and planform
# -----------------------------------------------------------------
## [designParameters]
despmtr   thick     0.12      frac of local chord
despmtr   camber    0.04      frac of local chord
despmtr   tlen      5.00      length from wing LE to Tail LE
despmtr   toff      0.5       tail offset

despmtr   area      10.0
despmtr   aspect    6.00
despmtr   taper     0.60
despmtr   sweep     20.0      deg (of c/4)

despmtr   washout  -5.00      deg (down at tip)
despmtr   dihedral  4.00      deg
## [designParameters]

# -----------------------------------------------------------------
# set parameters for use internally to create geometry
# -----------------------------------------------------------------
## [setParameters]
set       span      sqrt(aspect*area)
set       croot     2*area/span/(1+taper)
set       ctip      croot*taper
set       dxtip     (croot-ctip)/4+span/2*tand(sweep)
set       dztip     span/2*tand(dihedral)
## [setParameters]

# -----------------------------------------------------------------
# Wing Tip Store
# Notice two attributes:
# 	> capsType     - defines the type of awave component it is
#   > capsGroup			- unique string capsGroup used to group items together
# -----------------------------------------------------------------
## [createStore]
udprim 	ellipse ry 0.1*ctip rz 0.1*ctip
attribute capsType $Store
attribute capsGroup  $RightWingTank
translate dxtip    -span/2    dztip

udprim 	ellipse ry 0.1*ctip rz 0.1*ctip
attribute capsType $Store
attribute capsGroup  $RightWingTank
translate dxtip+ctip    -span/2    dztip

udprim 	ellipse ry 0.1*ctip rz 0.1*ctip
attribute capsType $Store
attribute capsGroup  $LeftWingTank
translate dxtip    span/2    dztip

udprim 	ellipse ry 0.1*ctip rz 0.1*ctip
attribute capsType $Store
attribute capsGroup  $LeftWingTank
translate dxtip+ctip    span/2    dztip
## [createStore]

# -----------------------------------------------------------------
# Wing
# Notice three attributes:
# 	> capsReferenceArea  - defines the reference area for coefficients
# 	> capsType     - defines the type of awave component it is
#  > capsGroup			- unique string capsGroup used to group items together
# -----------------------------------------------------------------
## [createWing]
# right tip
udprim    naca      Thickness thick     Camber    camber
attribute capsReferenceArea area
attribute capsType   $Wing
attribute capsGroup    $Wing
scale     ctip
rotatex   90        0         0
rotatey   washout   0         ctip/4
translate dxtip    -span/2    dztip

# root
udprim    naca      Thickness thick     Camber    camber
attribute capsType   $Wing
attribute capsGroup    $Wing
rotatex   90        0         0
scale     croot

# left tip
udprim    naca      Thickness thick     Camber    camber
attribute capsType  $Wing
attribute capsGroup    $Wing
scale     ctip
rotatex   90        0         0
rotatey   washout   0         ctip/4
translate dxtip     span/2    dztip
## [createWing]
# -----------------------------------------------------------------
# Vertical Tail
# Notice two attributes:
# 	> capsType     - defines the type of awave component it is
#  > capsGroup			- unique string capsGroup used to group items together
# -----------------------------------------------------------------
## [createVTail]
# tip
udprim    naca      Thickness thick
attribute capsType  $VTail
attribute capsGroup   $VertTail
scale     0.75*ctip
translate tlen+0.75*(croot-ctip) 0.0 ctip+toff

# base
udprim    naca      Thickness thick
attribute capsType  $VTail
attribute capsGroup   $VertTail
scale     0.75*croot
translate tlen 0.0 toff
## [createVTail]
# -----------------------------------------------------------------
# Horizontal Tail
# Notice two attributes:
# 	> capsType     - defines the type of awave component it is
#  > capsGroup			- unique string capsGroup used to group items together
# -----------------------------------------------------------------
## [createHTail]
# tip left
udprim    naca      Thickness thick
attribute capsType  $HTail
attribute capsGroup   $Stab
scale     0.75*ctip
rotatex   90        0         0
translate tlen+0.75*(croot-ctip) -ctip toff

# tip left
udprim    naca      Thickness thick
attribute capsType  $HTail
attribute capsGroup   $Stab
scale     0.75*ctip
rotatex   90        0         0
translate tlen+0.75*(croot-ctip) 0.0 toff

# tip right
udprim    naca      Thickness thick
attribute capsType  $HTail
attribute capsGroup   $Stab
scale     0.75*ctip
rotatex   90        0         0
translate tlen+0.75*(croot-ctip) ctip toff
## [createHTail]
# -----------------------------------------------------------------
# Fuselage
# Notice two attributes:
# 	> capsType     - defines the type of awave component it is
#  > capsGroup	   - unique string capsGroup used to group items together
# -----------------------------------------------------------------
## [createFuse]
skbeg -0.4*tlen 0.0 0.0
skend
attribute capsType $Fuse
attribute capsGroup  $Fuselage

udprim 	ellipse ry 0.5*croot rz 0.2*croot
attribute capsType $Fuse
attribute capsGroup  $Fuselage
translate 0.0 0.0 0.0

udprim 	ellipse ry 0.4*croot rz 0.1*croot
attribute capsType $Fuse
translate croot 0.0 0.0

udprim 	ellipse ry 0.1*croot rz 0.1*croot
attribute capsType $Fuse
attribute capsGroup  $Fuselage
translate tlen 0.0 toff

udprim 	ellipse ry 0.01*croot rz 0.01*croot
attribute capsType $Fuse
attribute capsGroup  $Fuselage
translate tlen+0.75*croot 0.0 toff
## [createFuse]

end
