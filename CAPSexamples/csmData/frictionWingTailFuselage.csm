# Example FRICTION input file to create a simple wing model

# -----------------------------------------------------------------
# Define the analysis fidelity that the geometry is intended support
# -----------------------------------------------------------------
## [capsAIM]
attribute capsAIM $frictionAIM
## [capsAIM]

# -----------------------------------------------------------------
# Friction Input is always in feet, capsLength defines the units this 
# *.csm file is in
# -----------------------------------------------------------------
## [capsLength]
attribute capsLength $m
## [capsLength]

# -----------------------------------------------------------------
# Design parameters to define the wing cross section and planform
# -----------------------------------------------------------------
## [designParameters]
despmtr   thick     0.12      frac of local chord
despmtr   camber    0.04      frac of loacl chord
despmtr   tlen      5.00      length from wing LE to Tail LE
despmtr   toff      0.5       tail offset

despmtr   area      10.0
despmtr   aspect    6.00
despmtr   taper     0.60
despmtr   sweep     20.0      deg (of c/4)

despmtr   washout  -5.00      deg (down at tip)
despmtr   dihedral  4.00      deg
## [designParameters]

# -----------------------------------------------------------------
# set parameters for use internally to create geometry
# -----------------------------------------------------------------
## [setParameters]
set       span      sqrt(aspect*area)
set       croot     2*area/span/(1+taper)
set       ctip      croot*taper
set       dxtip     (croot-ctip)/4+span/2*tand(sweep)
set       dztip     span/2*tand(dihedral)
## [setParameters]

# -----------------------------------------------------------------
# 1. Create Airfoils using the User Defined Primitive (UDP) naca
#  > The inputs used for this example to the UDP are Thickness and Camber
#  > Cross sections are in the X-Y plane and are rotated to the X-Z plane
# -----------------------------------------------------------------

## [createWing]
# right tip
udprim    naca      Thickness thick     Camber    camber
attribute capsReferenceArea area
attribute capsType   $Wing
scale     ctip
rotatex   90        0         0
rotatey   washout   0         ctip/4
translate dxtip    -span/2    dztip

# root
udprim    naca      Thickness thick     Camber    camber
attribute capsType   $Wing
rotatex   90        0         0
scale     croot

# left tip
udprim    naca      Thickness thick     Camber    camber
attribute capsType  $Wing
scale     ctip
rotatex   90        0         0
rotatey   washout   0         ctip/4
translate dxtip     span/2    dztip
## [createWing]

# -----------------------------------------------------------------
# vertical tail
# -----------------------------------------------------------------
## [createVTail]
# tip
udprim    naca      Thickness thick
attribute capsType  $VTail
scale     0.75*ctip
translate tlen+0.75*(croot-ctip) 0.0 ctip+toff

# root
udprim    naca      Thickness thick
attribute capsType  $VTail
scale     0.75*croot
translate tlen 0.0 toff
## [createVTail]

# -----------------------------------------------------------------
# horizontal tail
# -----------------------------------------------------------------
## [createHTail]
# tip left
udprim    naca      Thickness thick
attribute capsType  $HTail
scale     0.75*ctip
rotatex   90        0         0
translate tlen+0.75*(croot-ctip) -ctip toff

# tip right
udprim    naca      Thickness thick
attribute capsType  $HTail
scale     0.75*ctip
rotatex   90        0         0
translate tlen+0.75*(croot-ctip) ctip toff
## [createHTail]

# -----------------------------------------------------------------
# fuselage
# -----------------------------------------------------------------
## [createFuse]
point -0.4*tlen 0.0 0.0
attribute capsType $Fuse

udprim 	ellipse ry 0.5*croot rz 0.2*croot
attribute capsType $Fuse
translate 0.0 0.0 0.0

udprim 	ellipse ry 0.4*croot rz 0.1*croot
attribute capsType $Fuse
translate croot 0.0 0.0

udprim 	ellipse ry 0.1*croot rz 0.1*croot
attribute capsType $Fuse
translate tlen 0.0 toff

udprim 	ellipse ry 0.01*croot rz 0.01*croot
attribute capsType $Fuse
translate tlen+0.75*croot 0.0 toff
## [createFuse]

end
