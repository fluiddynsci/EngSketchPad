# tutorial6/model2 - simple tapered wing (with OML and AVL views)
# written by John Dannenhoffer

# modification of model1:
#     addition of taper, which is done by converting the EXTRUDE to a RULE
# modification of model2a:
#    addition of AVL view which puts the cross-sections used in the
#        RULE on the stack
#    this is done by STOREing the cross-sections in "wing -1"
# modification of model2b:
#    addition of ATTRIBUTEs needed by the AVL AIM

# view switch
CFGPMTR   view:OML      1
CFGPMTR   view:AVL      1

# design parameters
DESPMTR   wing:area    20.0
DESPMTR   wing:aspect   4.0
DESPMTR   wing:taper   1.00
DESPMTR   wing:thick   0.01
DESPMTR   wing:camber  0.06

# local variables
SET       wing:span    sqrt(wing:area*wing:aspect)
SET       wing:croot   wing:area/wing:span*2/(wing:taper+1)
SET       wing:ctip    wing:croot*wing:taper
SET       wing:xtip   (wing:croot-wing:ctip)/2

OUTPMTR   wing:span

# construct wing (left-to-right)
MARK
   # left tip
   UDPARG    naca         thickness wing:thick
   UDPARG    naca         camber    wing:camber
   UDPRIM    naca         sharpte   1
      ATTRIBUTE capsAIM           $avlAIM
      ATTRIBUTE capsIntent        $wing
      ATTRIBUTE capsGroup         $wing
   ROTATEX   90
   SCALE     wing:ctip
   TRANSLATE wing:xtip   -wing:span/2   0
   STORE     wing  -1  1

   # root
   UDPARG    naca         thickness wing:thick
   UDPARG    naca         camber    wing:camber
   UDPRIM    naca         sharpte   1
      ATTRIBUTE capsAIM           $avlAIM
      ATTRIBUTE capsIntent        $wing
      ATTRIBUTE capsGroup         $wing
   ROTATEX   90
   SCALE     wing:croot
   STORE     wing  -1  1

   # rite tip
   UDPARG    naca         thickness wing:thick
   UDPARG    naca         camber    wing:camber
   UDPRIM    naca         sharpte   1
      ATTRIBUTE capsAIM           $avlAIM
      ATTRIBUTE capsIntent        $wing
      ATTRIBUTE capsGroup         $wing
   ROTATEX   90
   SCALE     wing:ctip
   TRANSLATE wing:xtip   +wing:span/2   0
   STORE     wing  -1  1
RULE
   ATTRIBUTE capsGroup         $wing
STORE     OML

# to view AVL, restore the various sections
IFTHEN view:AVL NE 0
   RESTORE  wing -1
      ATTRIBUTE capsReferenceArea  wing:area
      ATTRIBUTE capsReferenceSpan  wing:span
      ATTRIBUTE capsReferenceChord wing:croot
      ATTRIBUTE capsReferenceX     wing:croot/4
ENDIF

# to view OML, restore the OML
IFTHEN view:OML NE 0
   RESTORE   OML
ENDIF

END
