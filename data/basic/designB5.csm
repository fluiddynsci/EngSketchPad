# designB5
# written by John Dannenhoffer

despmtr   DX   4.0
despmtr   DY   2.0
despmtr   DZ   5.0
despmtr   move 1.0
despmtr   bump 0.0
despmtr   rotn 0.0
despmtr   rad  2.0

set       DX2  DX+bump*2
set       DY2  DY+bump*2
set       DZ2  DZ+bump*2

# cylinder
cylinder   0  0  -1  0  0  +1  rad

# plate in XY plane (ruled in Z)
mark
udprim     box       dx  DX    dy  DY
translate  move      move     -DZ/2
udprim     box       dx  DX2   dy  DY2
rotatez    rotn      0         0
translate  move      move      0
udprim     box       dx  DX    dy  DY
translate  move      move     +DZ/2
rule
translate -move-2.5 -move-2.5  0
union

# plate in YZ plane (ruled in X)
mark
udprim     box       dy  DY    dz  DZ
translate -DX/2      move      move
udprim     box       dy  DY2   dz  DZ2
rotatex    rotn      0         0
translate  0         move      move
udprim     box       dy  DY    dz  DZ
translate +DX/2      move      move
rule
translate +2.5      -move-2.5 -move
union

# plate in ZX plane (ruled in Y)
mark
udprim     box       dz  DZ    dx  DX
translate  move     -DY/2      move
udprim     box       dz  DZ2   dx  DX2
rotatey    rotn      0         0
translate  move      0         move
udprim     box       dz  DZ    dx  DX
translate  move     +DY/2      move
rule
translate -move-2.5 +2.5      -move
union

# box
udprim     box       dx  DX    dy  DY    dz  DZ
translate +2.5      +2.5       0
union

end
