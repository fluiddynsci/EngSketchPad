# wingBEM
# written by John Dannenhoffer

# Design Parameters for OML
despmtr   thick     0.12      frac of local chord
despmtr   camber    0.04      frac of loacl chord

despmtr   area      10.0
despmtr   aspect    6.00
despmtr   taper     0.60
despmtr   sweep     20.0      deg (of c/4)

despmtr   washout   5.00      deg (down at tip)
despmtr   dihedral  4.00      deg

# Design Parameters for BEM
despmtr   nrib      11        number of ribs
despmtr   spar1     0.20      frac of local chord
despmtr   spar2     0.75      frac of local chord

# OML
set       span      sqrt(aspect*area)
set       croot     2*area/span/(1+taper)
set       ctip      croot*taper
set       dxtip     (croot-ctip)/4+span/2*tand(sweep)
set       dytip     span/2*tand(dihedral)

mark
   # rite tip
   udprim    naca      Thickness thick     Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     -span/2

   # root
   udprim    naca      Thickness thick     Camber    camber
   scale     croot

   # left tip
   udprim    naca      Thickness thick     Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     +span/2
rule
   attribute OML 1
select face 1
   attribute right_upper 1
select face 2
   attribute left_upper 1
select face 3
   attribute right_lower 1
select face 4
   attribute left_lower 1
select face 5
   attribute right_TE 1
select face 6
   attribute left_TE 1
select face 7
   attribute right_tip 1
select face 8
   attribute left_tip 1
store     OML

# wing ribs
dimension waffle  nrib+4 4 0
set       Nrib    nint(nrib)
patbeg    i Nrib
   set    waffle[i,1]     (span/2)*(2*i-Nrib-1)/Nrib
   set    waffle[i,2]     -0.01*croot
   set    waffle[i,3]     (span/2)*(2*i-Nrib-1)/Nrib
   set    waffle[i,4]     max(croot,dxtip+ctip)
patend

# wing spars
set       eps             0.01*span

set       waffle[Nrib+1,1]  -span/2-eps
set       waffle[Nrib+1,2]   spar1*ctip+dxtip
set       waffle[Nrib+1,3]   0
set       waffle[Nrib+1,4]   spar1*croot
set       waffle[Nrib+2,1]   span/2+eps
set       waffle[Nrib+2,2]   spar1*ctip+dxtip
set       waffle[Nrib+2,3]   0
set       waffle[Nrib+2,4]   spar1*croot
set       waffle[Nrib+3,1]  -span/2-eps
set       waffle[Nrib+3,2]   spar2*ctip+dxtip
set       waffle[Nrib+3,3]   0
set       waffle[Nrib+3,4]   spar2*croot
set       waffle[Nrib+4,1]   span/2+eps
set       waffle[Nrib+4,2]   spar2*ctip+dxtip
set       waffle[Nrib+4,3]   0
set       waffle[Nrib+4,4]   spar2*croot

udprim    waffle    Depth     +6*thick*croot      Segments  waffle
   attribute wing_structure 1
translate 0         0         -3*thick*croot
rotatey   90        0         0
rotatez  -90        0         0
store     waffle

# keep part of waffle inside OML
restore   waffle
restore   OML
intersect

# break the OML with the waffle and extract the shell
restore   OML
restore   waffle
subtract
extract   0

# sew the two SHEET Bodies together
union

# set the spacing along Edges 
udprim    createBEM filename  $wingBEM.bdf  space  0.20   imin   3   imax   99

end
