# color1b
# written by John Dannenhoffer

# Design Parameters:
despmtr   Lbar       6.00000
despmtr   Rbar       0.40000
despmtr   T          0.50000
despmtr   D          2.00000
despmtr   Rout       1.20000
despmtr   Rfil       0.10000
despmtr   Rhole      0.08000

# Local Variables:

# Global attributes:

# Branches:
set       L       Lbar/2

cylinder  -L   0.0   0.0   +L   0.0   0.0   Rbar
   name      shaft
   attribute shaft   1
   attribute _color  $red

box       -L-T/2   -D/3   -D   T   D*2/3   2*D
   name      left_weight
   attribute weight  1
   attribute _color  $green

union     0   0   0
fillet    Rfil   0
   attribute _color  $yellow

box       +L-T/2   -D/2   -D/2   T   D   D
   name      rite_weight
   attribute weight  2
   attribute _color  $green

union     0   0   0
fillet    Rfil   0
   attribute _color  $yellow

cylinder  -L-T   0.00   0.00   +L+T   0.00   0.00   Rout
   attribute clipper  10
   attribute _color  $blue

intersect none   1   0

sphere    1   0   0   2.3
   attribute _color  $magenta

intersect none   1   0

cone      0   0   0   1.2*L   0   0   0.2
   attribute _color  $cyan

subtract  none   1   0
patbeg    i 7
   cylinder  i/3   0   -1   i/3   0   +1   Rhole
      attribute _color  $white
      name      small_holes

   rotatex   -15*(i-1)   0   0
   subtract  none   1   0
patend

end
