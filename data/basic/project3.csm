# project3
# written by Marshall Galbraith

despmtr   rx       1.00
despmtr   ry       0.20
despmtr   offset   0.18

dimension points   3  4  0

# make half of the ellipse
udprim    ellipse   rx rx   ry ry
box       0 -2*ry -1 2*rx +4*ry +2
intersect

# revolve it about the y-axis
revolve   0  -ry  0  0  1  0  360
store   ellipse

# coarsen the mesh to stress PROJECT
restore ellipse
   attribute .tParam "0.4; 0.1; 75"
   attribute _name $Coarse

project    1.5*rx  offset  0.0    -1.0  0.0  0.0
set points[1,1]  @xcg
set points[2,1]  @ycg
set points[3,1]  @zcg
assert           @ycg  offset
assert           @zcg  0.0

project   -1.5*rx  offset  0.0     1.0  0.0  0.0
set points[1,2]  @xcg
set points[2,2]  @ycg
set points[3,2]  @zcg
assert           @ycg  offset
assert           @zcg  0.0

project    0.0     offset  1.5*rx  0.0  0.0 -1.0
set points[1,3]  @xcg
set points[2,3]  @ycg
set points[3,3]  @zcg
assert           @xcg  0.0
assert           @ycg  offset

project     0.0    offset -1.5*rx  0.0  0.0  1.0
set points[1,4]  @xcg
set points[2,4]  @ycg
set points[3,4]  @zcg
assert           @xcg  0.0
assert           @ycg  offset

# use a finer tessellation to visualize the surface
restore ellipse
   attribute _name $Fine
   attribute .tParam "0.1; 0.005; 20"

skbeg     -1.5*rx  offset  0.0
   linseg  1.5*rx  offset  0.0
skend
   attribute _name $Line1

skbeg      0.0  offset -1.5*rx
   linseg  0.0  offset  1.5*rx
skend
   attribute _name $Line2

patbeg     i        4
   point points[1,i]  points[2,i]  points[3,i]
      attribute _name !$Point+i
patend

end
