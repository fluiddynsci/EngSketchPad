# winding2
# written by John Dannenhoffer

DESPMTR   L   2.00  # length of core
DESPMTR   H   1.00  # height of core
DESPMTR   S   0.10  # spacing between windings
DESPMTR   D   0.08  # wire diameter
DESPMTR   N   10    # number of windings

# core
BOX       -L/2  -H/2   -2*D   L   H   4*D
   ATTRIBUTE _color $red

# insulator
BOX       -L/2-(N+1)*S  -H/2-(N+2)*S  -D  L+(2*N+2)*S  H+(2*N+3)*S  D
   ATTRIBUTE _color $blue

# wire cross-section
UDPRIM     ELLIPSE  ry  D/2  rz  D/2
TRANSLATE  -L/2  -H/2-S  0

# winding
SKBEG        -L/2      -H/2-S        0
   PATBEG I  N
      LINSEG +L/2      -H/2-I*S      0
      ARC    +L/2+I*S  -H/2          0  I*S*(1-1/sqrt(2))
      LINSEG +L/2+I*S  +H/2          0
      ARC    +L/2      +H/2+I*S      0  I*S*(1-1/sqrt(2))
      LINSEG -L/2      +H/2+I*S      0
      ARC    -L/2-I*S  +H/2          0  I*S*(1-1/sqrt(2))
      LINSEG -L/2-I*S  -H/2-S        0
      ARC    -L/2      -H/2-(I+1)*S  0  I*S*(1-1/sqrt(2))
   PATEND
SKEND

SWEEP

END
