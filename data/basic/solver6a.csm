# solver6a
# written by John Dannenhoffer

despmtr   L     3.0  # overall length
despmtr   H     4.0  # overall height

# initial values
set       x1    1.2
set       y1    1.0
set       x2    4.0
set       y2    1.3
set       x3    4.2
set       y3    5.2
set       x4    1.5
set       y4    5.2

# cnostraints
set       con1  $x1-1                                    # fixed x val
set       con2  $y1-1                                    # fixed y val
set       con3  $(x1-x2')*(x3-x2')'+(y1-y2')*(y3-y2')    # orthogonal at 2
set       con4  $x2-x1-L                                 # overall length
set       con5  $y4-y1-H                                 # overall height
set       con6  $x4-x1                                   # vertical seg 1-4
set       con7  $y2-y1                                   # horizontal seg 1-2
set       con8  $y4-y3                                   # horizontal seg 3-4

# solver
solbeg    x1;y1;x2;y2;x3;y3;x4;y4;
   solcon con1
   solcon con2
   solcon con3
   solcon con4
   solcon con5
   solcon con6
   solcon con7
   solcon con8
solend

# sketch
skbeg     x1   y1   0.0
   linseg x2   y2   0.0
   linseg x3   y3   0.0
   linseg x4   y4   0.0
   linseg x1   y1   0.0
skend

end
