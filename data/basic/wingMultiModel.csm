# wingMultiModel
# written by John Dannenhoffer

# Design Parameters for OML
despmtr   thick     0.12      frac of local chord
despmtr   camber    0.04      frac of loacl chord

despmtr   area      10.0
despmtr   aspect    6.00
despmtr   taper     0.60
despmtr   sweep     20.0      deg (of c/4)

despmtr   washout   5.00      deg (down at tip)
despmtr   dihedral  4.00      deg

# Design Parameters for BEM
despmtr   nrib      11        number of ribs
despmtr   spar1     0.20      frac of local chord
despmtr   spar2     0.75      frac of local chord

# Design Parameters for wake
despmtr   wakeLen   10.0      length of wake
despmtr   wakeAng   -5.0      deg

# OML
set       span      sqrt(aspect*area)
set       croot     2*area/span/(1+taper)
set       ctip      croot*taper
set       dxtip     (croot-ctip)/4+span/2*tand(sweep)
set       dytip     span/2*tand(dihedral)

mark
   # rite tip
   udprim    naca      Thickness thick     Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     -span/2

   # root
   udprim    naca      Thickness thick     Camber    camber
   scale     croot

   # left tip
   udprim    naca      Thickness thick     Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     +span/2
rule
   attribute name $OML
select face  @nbody  3
   attribute name $rite_upper
select face  @nbody  4
   attribute name $left_upper
select face  @nbody  5
   attribute name $rite_lower
select face  @nbody  6
   attribute name $left_lower
select face  @nbody  7
   attribute name $rite_TE
select face  @nbody  8
   attribute name $left_TE
select face  @nbody  1
   attribute name $rite_tip
select face  @nbody  2
   attribute name $left_tip
store     OML

# wing ribs
dimension waffle  nrib+4 4 0
set       Nrib    nint(nrib)
patbeg    i Nrib
   set    waffle[i,1]     (span/2)*(2*i-Nrib-1)/Nrib
   set    waffle[i,2]     -0.01*croot
   set    waffle[i,3]     (span/2)*(2*i-Nrib-1)/Nrib
   set    waffle[i,4]     max(croot,dxtip+ctip)
patend

# wing spars
set       eps             0.01*span

set       waffle[Nrib+1,1]  -span/2-eps
set       waffle[Nrib+1,2]   spar1*ctip+dxtip
set       waffle[Nrib+1,3]   0
set       waffle[Nrib+1,4]   spar1*croot
set       waffle[Nrib+2,1]   span/2+eps
set       waffle[Nrib+2,2]   spar1*ctip+dxtip
set       waffle[Nrib+2,3]   0
set       waffle[Nrib+2,4]   spar1*croot
set       waffle[Nrib+3,1]  -span/2-eps
set       waffle[Nrib+3,2]   spar2*ctip+dxtip
set       waffle[Nrib+3,3]   0
set       waffle[Nrib+3,4]   spar2*croot
set       waffle[Nrib+4,1]   span/2+eps
set       waffle[Nrib+4,2]   spar2*ctip+dxtip
set       waffle[Nrib+4,3]   0
set       waffle[Nrib+4,4]   spar2*croot

udprim    waffle    Depth     +6*thick*croot      Segments  waffle
   attribute name $wing_structure
translate 0         0         -3*thick*croot
rotatey   90        0         0
rotatez  -90        0         0
store     waffle

# keep part of waffle inside OML
restore   waffle
restore   OML
intersect

# break the OML with the waffle and extract the shell
restore   OML
restore   waffle
subtract
extract   0

# sew the two SheetBodies together
union     0  0  1e-5

# set the spacing along Edges 
udprim    createBEM filename  $wingMultiModel.bdf  space  0.20   imin   3   imax   99
store     BEM

# create the mid-surface-aero with wake
mark
   # rite tip
   udprim    naca      Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     -span/2

   # root
   udprim    naca      Camber    camber
   scale     croot

   # left tip
   udprim    naca      Camber    camber
   scale     ctip
   rotatez   washout   ctip/4    0
   translate dxtip     dytip     +span/2
rule
select face  @nbody  3
   attribute name $rite_camber
select face  @nbody  4
   attribute name $left_camber
store     MSA_wing
restore   MSA_wing

restore   MSA_wing
select    edge  0  0  35  3  3
extract   -@iedge
extrude   wakeLen*cosd(wakeAng)  wakeLen*sind(wakeAng)  0
union

restore   MSA_wing
select    edge  0  0  35  4  3
extract   -@iedge
extrude   wakeLen*cosd(wakeAng)  wakeLen*sind(wakeAng)  0
union
   attribute name $MSA
select    face  43  3
   attribute name $left_wake
select    face  39  3
   attribute name $rite_wake
store     MSA

# show all models at end
restore   OML
   attribute _name $OML
restore   BEM
   attribute _name $BEM
restore   MSA
   attribute _name $MSA

end
