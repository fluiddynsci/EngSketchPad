# evaluate2
# written by John Dannenhoffer

dimension foo 20 1

mark
   udprim    supell rx   2 \
                    ry_s 4 \
                    ry_n 2 \
                    n_ne 5
   translate 0 0 -6

   udprim    supell rx   4 \
                    ry_s 4 \
                    ry_n 2 \
                    n_s  4

   udprim    supell rx   2 \
                    ry   2 \
                    n_sw 5
   translate 0 0 4
blend

# Node locations
patbeg inode @nnode
   evaluate  node  @ibody inode
   assert    3     @edata.size
   set       foo   @edata
patend

# Edge midpoints
patbeg iedge @nedge
   evaluate  edge  @ibody iedge $beg
   assert    10    @edata.size
   set       tmin  @edata[1]

   evaluate  edge  @ibody iedge $end
   assert    10    @edata.size
   set       tmax  @edata[1]

   set       tmid (tmin+tmax)/2

   evaluate  edge  @ibody iedge tmid
   assert    10    @edata.size
   set       foo   @edata

   evaluate  edgeinv @ibody iedge @edata[2] @edata[3] @edata[4]
   assert    4       @edata.size
   set       foo     @edata

   assert    @edata[1] tmid
patend

# Face midpoints
patbeg iface @nface
   evaluate  face  @ibody iface -99999 -99999
   assert    20    @edata.size
   set       umin  @edata[1]
   set       vmin  @edata[2]

   evaluate  face  @ibody iface +99999 +99999
   assert    20    @edata.size
   set       umax  @edata[1]
   set       vmax  @edata[2]

   set       umid  (umin+umax)/2
   set       vmid  (vmin+vmax)/2

   evaluate  face  @ibody iface umid vmid
   assert    20    @edata.size
   set       foo   @edata

   evaluate  faceinv @ibody iface @edata[3] @edata[4] @edata[5]
   assert    5       @edata.size
   set       foo     @edata

   assert   @edata[1] umid
   assert   @edata[2] vmid
patend

end
