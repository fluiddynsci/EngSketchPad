# wingFuseBEM1
# written by John Dannenhoffer

despmtr   generateWing 1
despmtr   generateFuse 1

# waffle for wing and fuselage
dimension waffle   13  4  0

# spar 1
set       waffle[1,1]   4.00
set       waffle[1,2]  -3.00
set       waffle[1,3]   6.00
set       waffle[1,4]  -12.0

# spar 2
set       waffle[2,1]   6.00
set       waffle[2,2]  -3.00
set       waffle[2,3]   7.00
set       waffle[2,4]  -12.0

# rib 1
set       waffle[3,1]   3.00
set       waffle[3,2]  -4.50
set       waffle[3,3]   8.00
set       waffle[3,4]  -4.50

# rib 2
set       waffle[4,1]   3.00
set       waffle[4,2]  -6.50
set       waffle[4,3]   8.00
set       waffle[4,4]  -6.50

# rib 3
set       waffle[5,1]   3.00
set       waffle[5,2]  -8.50
set       waffle[5,3]   8.00
set       waffle[5,4]  -8.50

# rib 4
set       waffle[6,1]   3.00
set       waffle[6,2]  -10.5
set       waffle[6,3]   8.00
set       waffle[6,4]  -10.5

# fuselage centerline
set       waffle[7,1]   0.00
set       waffle[7,2]   0.00
set       waffle[7,3]   12.0
set       waffle[7,4]   0.00

# frame 1
set       waffle[8,1]   2.00
set       waffle[8,2]  -3.00
set       waffle[8,3]   2.00
set       waffle[8,4]   3.00

# frame 2
set       waffle[9,1]   4.00
set       waffle[9,2]  -3.00
set       waffle[9,3]   4.00
set       waffle[9,4]   3.00

# frame 3
set       waffle[10,1]   5.00
set       waffle[10,2]  -3.00
set       waffle[10,3]   5.00
set       waffle[10,4]   3.00

# frame 4
set       waffle[11,1]   6.00
set       waffle[11,2]  -3.00
set       waffle[11,3]   6.00
set       waffle[11,4]   3.00

# frame 5
set       waffle[12,1]   8.00
set       waffle[12,2]  -3.00
set       waffle[12,3]   8.00
set       waffle[12,4]   3.00

# frame 6
set       waffle[13,1]   10.0
set       waffle[13,2]  -3.00
set       waffle[13,3]   10.0
set       waffle[13,4]   3.00

udparg    waffle    Depth     4.0
udprim    waffle    Segments  waffle
translate 0  0  -2

set       yy      2.00/tand(60)
set       zz      3.00*tand(30)

# add longeron 1
skbeg     0.00   -yy     -2.00
   linseg 12.0   -yy     -2.00
   linseg 12.0    yy      2.00
   linseg 0.00    yy      2.00
   linseg 0.00   -yy     -2.00
skend
union

# add longeron 2
skbeg     0.00   -3.00   -zz
   linseg 12.0   -3.00   -zz
   linseg 12.0    3.00    zz
   linseg 0.00    3.00    zz
   linseg 0.00   -3.00   -zz
skend
union

# add longeron 3
skbeg     0.00   -3.00    0.00
   linseg 12.0   -3.00    0.00
   linseg 12.0    3.00    0.00
   linseg 0.00    3.00    0.00
   linseg 0.00   -3.00    0.00
skend
union

# add longeron 4
skbeg     0.00   -yy      2.00
   linseg 12.0   -yy      2.00
   linseg 12.0    yy     -2.00
   linseg 0.00    yy     -2.00
   linseg 0.00   -yy      2.00
skend
union

# add longeron 5
skbeg     0.00   -3.00    zz
   linseg 12.0   -3.00    zz
   linseg 12.0    3.00   -zz
   linseg 0.00    3.00   -zz
   linseg 0.00   -3.00    zz
skend
union
store     waffle

# OML for wing
ifthen generateWing EQ 1
   mark
      udprim    naca      series    4412
      rotatex   90   0  0
      scale     3.0
      translate 3.5  -2.8  0.5

      udprim    naca      series    4412
      rotatex   90   0   0
      scale     1.8
      translate 5.5   -11.0   0.5
   rule
   store     wingOML
endif

# OML for fuselage
ifthen generateFuse EQ 1
   mark
      udparg    supell    rx        1.9
      udparg    supell    ry        2.9
      udprim    supell    n         4.0
      rotatey    90   0  0
      translate 0.1   0  0

      udparg    supell    rx        1.9
      udparg    supell    ry        2.9
      udprim    supell    n         4.0
      rotatey   90    0  0
      translate 11.9  0  0
   rule
   store     fuseOML

   # hollow out fuselage
   restore   fuseOML
   hollow    0.2   5;6   1
   store     fuseOML/IML
endif

# overall OML
ifthen generateWing EQ 1 AND generateFuse EQ 1
   restore   wingOML
   restore   fuseOML
   union
   store     allOML
elseif generateWing EQ 1
   restore   wingOML
   store     allOML
elseif generateFuse EQ 1
   restore   fuseOML
   store     allOML
else
   assert 0 1
endif

# overall OML/IML
ifthen generateWing EQ 1 AND generateFuse EQ 1
   restore   wingOML
   restore   fuseOML/IML
   union
   store     allOML/IML
elseif generateWing EQ 1
   restore   wingOML
   store     allOML/IML
elseif generateFuse EQ 1
   restore   fuseOML/IML
   store     allOML/IML
endif

# trim waffle by OML
restore   waffle
restore   allOML/IML
intersect
store     structure

# scribe skin by waffle
restore   allOML
restore   waffle
subtract
extract   0
store     skins

# union skins with structure
restore   structure
restore   skins
union

end
