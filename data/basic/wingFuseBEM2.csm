# wingFuseBEM2
# written by John Dannenhoffer

despmtr   generateWing 1
despmtr   generateFuse 1

# waffle for wing
ifthen generateWing EQ 1
   dimension waffle    6  4  0

   # spar 1
   set       waffle[1,1]   4.00
   set       waffle[1,2]  -3.00
   set       waffle[1,3]   6.00
   set       waffle[1,4]  -12.0

   # spar 2
   set       waffle[2,1]   6.00
   set       waffle[2,2]  -3.00
   set       waffle[2,3]   7.00
   set       waffle[2,4]  -12.0

   # rib 1
   set       waffle[3,1]   3.00
   set       waffle[3,2]  -4.50
   set       waffle[3,3]   8.00
   set       waffle[3,4]  -4.50

   # rib 2
   set       waffle[4,1]   3.00
   set       waffle[4,2]  -6.50
   set       waffle[4,3]   8.00
   set       waffle[4,4]  -6.50

   # rib 3
   set       waffle[5,1]   3.00
   set       waffle[5,2]  -8.50
   set       waffle[5,3]   8.00
   set       waffle[5,4]  -8.50

   # rib 4
   set       waffle[6,1]   3.00
   set       waffle[6,2]  -10.5
   set       waffle[6,3]   8.00
   set       waffle[6,4]  -10.5

   udparg    waffle    Depth     4.0
   udprim    waffle    Segments  waffle
   translate 0  0  -2
   store     wingWaffle
endif

# fuselage structure
ifthen generateFuse EQ 1
   udparg    radwaf    ysize     6.0
   udparg    radwaf    zsize     4.0
   udparg    radwaf    nspoke    12
   udprim    radwaf    xframe    "0;2;4;5;6;8;10;12"
   store     fuseWaffle
endif

ifthen generateWing EQ 1 AND generateFuse EQ 1
   restore   wingWaffle
   restore   fuseWaffle
   join
   store     waffle
elseif generateWing EQ 1
   restore   wingWaffle
   store     waffle
elseif generateFuse EQ 1
   restore   fuseWaffle
   store     waffle
else
   assert 0 1
endif

# OML for wing
ifthen generateWing EQ 1
   mark
      udprim    naca      series    4412
      rotatex   90   0  0
      scale     3.0
      translate 3.5  -2.8  0.5

      udprim    naca      series    4412
      rotatex   90   0   0
      scale     1.8
      translate 5.5   -11.0   0.5
   rule
   store     wingOML
endif

# OML for fuselage
ifthen generateFuse EQ 1
   mark
      udparg    supell    rx        1.9
      udparg    supell    ry        2.9
      udprim    supell    n         4.0
      rotatey    90   0  0
      translate 0.1   0  0

      udparg    supell    rx        1.9
      udparg    supell    ry        2.9
      udprim    supell    n         4.0
      rotatey   90    0  0
      translate 11.9  0  0
   rule
   store     fuseOML

   # hollow out fuselage
   restore   fuseOML
   hollow    0.2   5;6   1
   store     fuseOML/IML
endif

# overall OML
ifthen generateWing EQ 1 AND generateFuse EQ 1
   restore   wingOML
   restore   fuseOML
   union
   store     allOML
elseif generateWing EQ 1
   restore   wingOML
   store     allOML
elseif generateFuse EQ 1
   restore   fuseOML
   store     allOML
else
   assert 0 1
endif

# overall OML/IML
ifthen generateWing EQ 1 AND generateFuse EQ 1
   restore   wingOML
   restore   fuseOML/IML
   union
   store     allOML/IML
elseif generateWing EQ 1
   restore   wingOML
   store     allOML/IML
elseif generateFuse EQ 1
   restore   fuseOML/IML
   store     allOML/IML
endif

# trim waffle by OML
restore   waffle
restore   allOML/IML
intersect
store     structure

# scribe skin by waffle
restore   allOML
restore   waffle
subtract
extract   0
store     skins

# union skins with structure
restore   structure
restore   skins
union

end
