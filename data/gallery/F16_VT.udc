# F16_VT
# written by Chris Meckstroth

interface  deflRud  in   0
set        fntGap        2.0
set        sidGap        2.0
set        rudMove           0.01

dimension  VT     3  5  0
#                       x        y         z      chord     angle
set        VT      " 233.559;   0.000;    0.000;  300.982;    0.000;\
                     436.539;   0.000;   57.924;   98.002;    0.000;\
                                         526.669;   0.000;  140.182;   47.855;    0.000; "
                                         
dimension  Thick  1  3  0
set        Thick   " 0.0541;  0.0434;  0.0300; "

dimension rud     2  2  0
#                   x         z
set       rud  " 505.461;   57.924;\
                 556.258;  134.182; "

### Build Clean Vertical Tail ###                                
mark

udprim biconvex thick    Thick[1,1]
scale   VT[1,4]
translate   VT[1,1]  VT[1,2]  VT[1,3]

#udprim biconvex thick    0.0541
#scale   169.902
#translate   364.639     0.000    37.406

udprim biconvex thick    Thick[1,2]
scale    VT[2,4]
translate   VT[2,1]  VT[2,2]  VT[2,3]

udprim biconvex thick    Thick[1,3]
scale    VT[3,4]
translate   VT[3,1]  VT[3,2]  VT[3,3]

rule

store VTClean 0 1

### Build and Add Rudder ###
# Interpolate x position of rudder LE at root + gap
set    x0R       rud[1,1]
set    x1R       rud[2,1]
set    z0R       rud[1,2]
set    z1R       rud[2,2]

# Create Rudder #
skbeg      x0R+fntGap  -20.0   z0R
   linseg  x0R+100.0   -20.0   z0R
   linseg  x0R+100.0   -20.0   z1R
   linseg  x1R+fntGap  -20.0   z1R
   linseg  x0R+fntGap  -20.0   z0R
skend
extrude    0.0  40.0  0.0
intersect
store Rudder
restore VTClean

skbeg      x0R        -20.0   z0R
   linseg  x0R+100.0  -20.0   z0R
   linseg  x0R+100.0  -20.0   z1R
   linseg  x1R        -20.0   z1R
   linseg  x0R        -20.0   z0R
skend

extrude    0.0  40.0  0.0

subtract
restore Rudder

# Rotate Rudder About Axis 
udparg $$/gen_rot xbeg    x0R      ybeg  0.0  zbeg  z0R
udparg $$/gen_rot xend    x1R      yend  0.0  zend  z1R
udparg $$/gen_rot rotang  deflRud
udprim $$/gen_rot

connect "7" "4"

# Cut Connection Points To Limit Sharp Edges
set    xint10R    x0R+(z0R-sidGap/2-z0R)*(x1R-x0R)/(z1R-z0R)
set    xint11R    x0R+(z0R+sidGap/2-z0R)*(x1R-x0R)/(z1R-z0R)
set    xint20R    x0R+(z1R-sidGap/2-z0R)*(x1R-x0R)/(z1R-z0R)
set    xint21R    x0R+(z1R+sidGap/2-z0R)*(x1R-x0R)/(z1R-z0R)

skbeg     xint10R-rudMove       -20.0  z0R-sidGap/2
   linseg xint10R+100-rudMove   -20.0  z0R-sidGap/2
   linseg xint10R+100-rudMove   -20.0  z0R+sidGap/2
   linseg xint11R-rudMove       -20.0  z0R+sidGap/2
   linseg xint10R-rudMove       -20.0  z0R-sidGap/2
skend

extrude 0.0  40.0  0.0

subtract

skbeg     xint20R-rudMove       -20.0  z1R-sidGap/2
   linseg xint20R+100-rudMove   -20.0  z1R-sidGap/2
   linseg xint20R+100-rudMove   -20.0  z1R+sidGap/2
   linseg xint21R-rudMove       -20.0  z1R+sidGap/2
   linseg xint20R-rudMove       -20.0  z1R-sidGap/2
skend

extrude 0.0  40.0  0.0

subtract
