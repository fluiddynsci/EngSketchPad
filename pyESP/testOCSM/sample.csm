# sample (for testing pyOcsm)
# written by John Dannenhoffer

CFGPMTR   oper      0   # 1=union, 2=subtract, 3=intersect

DESPMTR   x0        1
DESPMTR   y0        2
DESPMTR   z0        3
DESPMTR   dx        4
DESPMTR   dy        3
DESPMTR   dz        2
DESPMTR   rad       0
DESPMTR   tfact     0.5

# global Attribute
ATTRIBUTE globAttr  12345

BOX       x0  y0  z0  dx  dy  dz

STORE     myBody
RESTORE   myBody

# Body 2 should get tessellated when sensitivities are computed
OUTPMTR   myLength
SET       myLength     @length

OUTPMTR   myArea
SET       myArea       @area

OUTPMTR   myVolume
SET       myVolume     @volume

DIMENSION myCG         1 3
OUTPMTR   myCG
SET       myCG[1]      @xcg*1
SET       myCG[2]      @ycg
SET       myCG[3]      @zcg

DIMENSION myBbox       2 3
OUTPMTR   myBbox
SET       myBbox[1,1]  @xmin
SET       myBbox[1,2]  @ymin
SET       myBbox[1,3]  @zmin
SET       myBbox[2,1]  @xmax
SET       myBbox[2,2]  @ymax
SET       myBbox[2,3]  @zmax

OUTPMTR   title
SET       title        $This_is_the_title

# Body 3 should get tessellated to display in ESP
STORE     myBody
RESTORE   myBody

# make the cylinder if a positive radius
IFTHEN    rad  GT  0
   CYLINDER  x0+dx/2   y0-1   z0+dz   x0+dx/2   y0+dy+1   z0+dz   rad
   NAME      cylinder
   ATTRIBUTE _color  $red

   # adjust cylinder's tessellation
   IFTHEN    tfact  NE  1
      UDPRIM  $$/applyTparams  factor  tfact
   ENDIF

   IFTHEN    oper  EQ  UNION
      MESSAGE  Bodys_are_unioned
      UNION
   ELSEIF    oper  EQ  SUBTRACT
      MESSAGE  Bodys_are_subtracted
      SUBTRACT
   ELSEIF    oper  EQ  INTERSECT
      MESSAGE  Bodys_are_intersected
      INTERSECT
   ELSE
      MESSAGE  Bodys_are_not_combined
   ENDIF
ENDIF

END
