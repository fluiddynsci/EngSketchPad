# CSM file example of two wings with an outer bounding box 

## [capsLength]
# lenght units in ft
ATTRIBUTE capsLength $ft
## [capsLength]

# Constant pramaters
CONPMTR  nConst  10

# Design Parameters:
## [despmtrs]
CFGPMTR   series            8412
CFGPMTR   series2           0020
DESPMTR   area[1,1]         40.00000
DESPMTR   aspect[1,1]       5.00000
DESPMTR   taper[1,1]        0.50000
DESPMTR   twist[1,1]        15.00000
DESPMTR   wing:lesweep[1,1]      30.00000
DESPMTR   wing:dihedral[1,1]     1.00000
DESPMTR   sphereR            80.00000

CFGPMTR   VIEW:CFD            1

DESPMTR v@1:d_name     1  # Used to test varaible names
## [despmtrs]

## [bounds]
LBOUND taper 0
UBOUND taper 1
## [bounds]

# Output Parameters:
## [outpmtrs]
OUTPMTR  span
OUTPMTR  dummyRow
OUTPMTR  dummyRow2
OUTPMTR  dummyRow3
OUTPMTR  dummyCol
OUTPMTR  dummyMat
OUTPMTR  cmean
## [outpmtrs]

## [capsAIM]
ATTRIBUTE capsAIM    $fun3dAIM;su2AIM;egadsTessAIM;aflr4AIM;tetgenAIM;aflr3AIM;masstranAIM
## [capsAIM]


## [capsAttributes]
# Set reference values
ATTRIBUTE capsReferenceArea     area
ATTRIBUTE capsReferenceChord    sqrt(area/aspect)
ATTRIBUTE capsReferenceSpan     sqrt(area/aspect)*aspect
## [capsAttributes]

# Dummy variables for testing 
CFGPMTR   nrow     3
CFGPMTR   ncol     2

DIMENSION despMat  nrow ncol
DESPMTR   despMat  "11;12;\
                    13;14;\
                    15;16;"

DIMENSION despCol  3 1
DESPMTR   despCol  "11;\
                    13;\
                    15;"

DIMENSION despRow  1 3
DESPMTR   despRow  "11;12;13"

DIMENSION dummyRow   3 1 0
DIMENSION dummyRow2  3 1 0
DIMENSION dummyRow3  5 1 0
DIMENSION dummyCol   1 3 0
DIMENSION dummyMat   3 2 0



SET dummyRow    "1;2;3;"
SET dummyRow2   "1;2"
SET dummyCol    "3;2;1;"
SET dummyMat    "1;2;\
                 3;4;\
                 5;6;"

SET dummyRow3[0] 1. # sets all entries to 1.
SET dummyRow3[1] 2.
SET dummyRow3[2] 3.


                                  
# Branches:
## [local]
SET       cmean     sqrt(area/aspect)
SET       span      cmean*aspect
SET       sspan     span/2
SET       croot     2*cmean/(1+taper)
SET       ctip      croot*taper
SET       xtip      sspan*tand(wing:lesweep)
SET       ytip      sspan*tand(wing:dihedral)
SET       ybot      -0.1*croot
SET       ytop      +0.2*croot+ytip
SET       extend    0.02*cmean
## [local]

## [createWing]
MARK
   UDPRIM    naca   Series   series
      SCALE     croot
   UDPRIM    naca   Series   series2
      SCALE     ctip
      ROTATEZ   -twist   0   0
      TRANSLATE xtip   ytip   -sspan
RULE      0
## [createWing]


## [mirror]
# Store half of wing 
STORE     HalfWing 0 0

# Restore the half wing 
RESTORE   HalfWing 0

# Restore and mirror the half wing
RESTORE   HalfWing 0
    MIRROR    0   0   1   0
## [mirror]

## [join]
# Combine halfs into a whole
JOIN     0
## [join]

## [orient]
# Get body into a typical aero-system 
ROTATEX 90 0 0
## [orient]

## [attribute]
# Store the wing 
STORE     Wing 0 0

MARK

# Wing 1 - Restore
RESTORE   Wing 0
    ATTRIBUTE _name $Wing1
    ATTRIBUTE capsGroup $Wing1
    ATTRIBUTE capsIntent $OML;CFD
    ATTRIBUTE .tParam    "5.0; 5.0/15; 35"
                             #iface;ubar0;vbar0;du2;dv2
    CSYSTEM   leftWingSkin   5;0;0;0;1
    CSYSTEM   riteWingSkin   1;0;0;0;1

SELECT face 7 3 1
    ATTRIBUTE capsBound $Upper_Left
## [attribute]

##[secondWing]
# Wing 2 -  Restore and scale, translate
RESTORE   Wing 0
    ATTRIBUTE _name $Wing2
    ATTRIBUTE capsGroup $Wing2
    ATTRIBUTE capsIntent $OML;CFD
    ATTRIBUTE .tParam    "5.0; 5.0/15; 35"
    
    SCALE     0.4
    TRANSLATE 10   0   0
##[secondWing]

# Create a bounding box as a farfied 
## [farfield]
SPHERE    0   0   0   sphereR
    ATTRIBUTE _name $Farfield
    ATTRIBUTE capsGroup $Farfield
    ATTRIBUTE capsIntent $Farfiled;CFD
    ATTRIBUTE .tParam    "50; 50/15; 35"
## [farfield]

OUTPMTR sphereVol
set sphereVol @volume

# Dump out an egads file to test reading parameters
DUMP unitGeom.egads 0 1

## [closeCSM]
END
## [closeCSM]
